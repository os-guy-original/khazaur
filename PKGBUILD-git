# Maintainer: os-guy-original <https://github.com/os-guy-original>
pkgname=khazaur-git
pkgver=r0.0.0
pkgrel=1
pkgdesc="Unified package manager for Arch Linux (git version)"
arch=('x86_64')
url="https://github.com/os-guy-original/khazaur"
license=('GPL3')
depends=('pacman' 'sudo')
makedepends=('rust' 'cargo' 'git')
optdepends=(
    'flatpak: for Flatpak application support'
    'snapd: for Snap package support'
    'debtap: for Debian package conversion'
    'git: for faster AUR package downloads'
)
provides=('khazaur')
conflicts=('khazaur')
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
    cd "${pkgname%-git}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${pkgname%-git}"
    cargo build --release
}

check() {
    cd "${pkgname%-git}"
    cargo test --release
}

package() {
    cd "${pkgname%-git}"
    
    # Install binary
    install -Dm755 "target/release/khazaur" "$pkgdir/usr/bin/khazaur"
    
    # Install documentation
    install -Dm644 README.md "$pkgdir/usr/share/doc/khazaur/README.md"
    install -Dm644 docs/COMMANDS.md "$pkgdir/usr/share/doc/khazaur/COMMANDS.md"
    install -Dm644 docs/CONFIGURATION.md "$pkgdir/usr/share/doc/khazaur/CONFIGURATION.md"
    install -Dm644 docs/RETRY.md "$pkgdir/usr/share/doc/khazaur/RETRY.md"
    
    # Install license
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/khazaur/LICENSE"
}
